// Copyright 2023-2024 The Jule Programming Language.
// Use of this source code is governed by a BSD 3-Clause
// license that can be found in the LICENSE file.

use std::jule::lex::{Token, TokenId}

// Trait.
struct Trait {
    Token:       &Token
    Ident:       str
    Public:      bool
    Mutable:     bool
    Inherits:    []&TypeSymbol
    Methods:     []&Fn
    Implemented: []&Struct
}

impl Kind for Trait {
    // Implement: Kind
    // Returns Trait's identifier.
    fn Str(self): str {
        ret self.Ident
    }

    // Reports whether types are same.
    fn Equal(&self, other: &TypeKind): bool {
        let trt = unsafe { (*(&other)).Trait() }
        ret self == trt
    }
}

impl Trait {
    // Reports whether Trait is built-in.
    fn IsBuiltin(self): bool {
        ret self.Token == nil
    }

    // Returns method by identifier.
    // Returns nil if not exist any method in this identifier.
    fn FindMethod(mut self, ident: str): &Fn {
        for (_, mut f) in self.Methods {
            if f.Ident == ident {
                ret f
            }
        }
        ret self.findMethodInherit(ident)
    }

    fn inherits(mut &self, &t: &Trait): bool {
        if self == t {
            ret true
        }
        for (_, mut i) in self.Inherits {
            if i.Kind.Trait().inherits(t) {
                ret true
            }
        }
        ret false
    }

    fn findMethodInherit(mut self, &ident: str): &Fn {
        for (_, mut i) in self.Inherits {
            if i.Kind == nil {
                continue
            }
            let mut t = i.Kind.Trait()
            if t != nil {
                let mut f2 = t.FindMethod(ident)
                if f2 != nil {
                    ret f2
                }
            }
        }
        ret nil
    }
}